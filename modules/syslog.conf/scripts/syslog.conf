#!/usr/bin/perl -w

use strict;
use warnings;
use Scripts qw/:all/;

my %file = args()
  or die;

# detect syslog implementation based on OS
my ( $syslog, $from, $to, $prefix, $cmd );

if( $^O eq 'linux' && get_redhat_release() >= 6 ) {
    # assume rsyslog
    $syslog = 'rsyslog';
    $from   = 'linux';
    $to     = '/etc/rsyslog.conf';
    $prefix = <<'EOT';
#rsyslog v3 config file

# provides support for local system logging (e.g. via logger command)
$ModLoad imuxsock.so

# provides kernel logging support (previously done by rklogd)
$ModLoad imklog.so

# Use default timestamp format
$ActionFileDefaultTemplate RSYSLOG_TraditionalFileFormat

EOT
    $cmd    = restart_cmd( service => 'rsyslog' );
} else {
    # assume syslog
    $syslog = 'syslog';
    $from   = "$^O";
    $to     = '/etc/syslog.conf';
    $cmd =
         restart_cmd( service => 'syslog' )
      || restart_cmd( service => 'syslogd' )
      || restart_cmd( service => 'syslog', reload_ok => 1 );
}

$cmd or die "can't figure out how to restart syslog";
$file{$from} or die "can't find src for $from";

if( $ENV{'CHISEL_DEBUG'} ) {
    warn "syslog.conf: copy from [$file{$from}] to [$to] with cmd [$cmd]\n";
}

# read contents of syslog.conf
my $contents = read_file( filename => $file{$from} );

# add prefix if needed
if( defined $prefix ) {
    $contents = $prefix . $contents;
}

write_file(
    contents => $contents,
    filename => $to,
    cmd      => $cmd,
);

# For legacy reasons /var/log/all has always been world readable.
# Some tools like groucho depend on the ability to parse /var/log/all
# as a non-root user. However, in the ylinux security rpm we are
# enforcing 0600 readable by root.  In order to keep monitoring
# functional we need to have different standards

if( $^O eq 'linux' ) {
    Scripts::chmod_file 0600, "/var/log/all"
      if -f "/var/log/all";
} elsif( $^O eq 'freebsd' ) {
    Scripts::chmod_file 0644, "/var/log/all"
      if -f "/var/log/all";
}

exit 0;
