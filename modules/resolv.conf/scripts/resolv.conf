#!/usr/bin/perl -w

use strict;
use Socket qw/inet_aton/;
use Scripts qw/:all/;

my %file = args()
  or die;

# should we shuffle 'nameserver' lines?
my $rotate = $file{rotate} && read_file( filename => $file{rotate} );

# read, then write
my $contents = read_file( filename => $file{MAIN} );

if( $rotate ) {
    # rotate based on IP address so it doesn't change too much
    my $ip = get_my_ip();
    $ip &&= unpack "N", inet_aton($ip);
    
    if( $ip ) { # only rotate if IP could be determined
        my ( @nameserver, @rest );
        foreach my $c (split "\n", $contents) {
            if( $c =~ /^nameserver\s+([\w\.]+)/ && $1 ne 'localhost' && $1 ne '127.0.0.1' && $1 ne '::1' ) {
                push @nameserver, $c;
            } else {
                push @rest, $c;
            }
        }

        if( @nameserver ) {
            my $first_nameserver = $ip % scalar @nameserver;
            unshift @nameserver, splice @nameserver, $first_nameserver, 1;
        }

        $contents = join "", map "$_\n", @rest, @nameserver;
    }
}

write_file(contents => $contents,
           filename => "/etc/resolv.conf",
           test     => "host transport | grep 'has address'");
